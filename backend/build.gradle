// ==================================================
// プラグイン
// ==================================================
plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '7.0.4'
    id 'com.github.spotbugs' version '6.0.27'
    id 'jacoco'
}

// ==================================================
// プロジェクト情報
// ==================================================
group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

// ==================================================
// 依存関係
// ==================================================
dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.flywaydb:flyway-database-postgresql'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Runtime
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-actuator-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:testcontainers-elasticsearch'
    testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers-postgresql'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // SpotBugs
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0'
}

// ==================================================
// コード品質ツール設定
// ==================================================

// --- Checkstyle ---
checkstyle {
    toolVersion = '13.2.0'
    configFile = file("${rootDir}/checkstyle.xml")
    maxWarnings = 0
}

// --- Spotless ---
spotless {
    java {
        eclipse().configFile("${rootDir}/.formatter/eclipse-formatter.xml")
        target 'src/*/java/**/*.java'
    }
}

// --- SpotBugs ---
spotbugs {
    effort = 'max'
    reportLevel = 'medium'
}

// --- JaCoCo ---
jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
    }
}

// ==================================================
// タスク設定
// ==================================================

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

// テストコードにJavadocを強制しない
tasks.named('checkstyleTest') {
    enabled = false
}

// CheckstyleがJDK25に未対応のためJDK23で実行
tasks.withType(Checkstyle).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

// SpotBugsがJDK25に未対応のため一時的に無効化
tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    enabled = false
}

// @RequestParamのパラメータ名をリフレクションで取得
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add('-parameters')
}
